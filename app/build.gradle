apply plugin: 'com.android.application'

android {
    compileSdkVersion appCompileSdkVersion
    buildToolsVersion appBuildToolsVersion

    defaultConfig {
        applicationId "org.cryse.lkong"
        minSdkVersion appMinSdkVersion
        targetSdkVersion appTargetSdkVersion
        versionCode appVersionCode
        versionName appVersionName
        jackOptions {
            enabled true
        }
    }

    dexOptions { jumboMode = true }

    compileOptions {
        encoding 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        debug {}
        release {}
    }

    buildTypes {
        debug {
            debuggable true
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            // shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        playStore {
            resValue 'string', 'track_channel_name', 'playStore'
            buildConfigField "boolean", "InAppUpdate", "true"
        }
    }

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
}

dependencies {
    compile libDependencies['support-v13']
    compile libDependencies['design']
    compile libDependencies['appcompat-v7']
    compile libDependencies['recyclerview-v7']
    compile libDependencies['cardview-v7']

    compile libDependencies['easypermissions']
    compile libDependencies['gson']
    compile libDependencies['okhttp']

    compile libDependencies['rxjava']
    compile libDependencies['rxandroid']
    compile libDependencies['butterknife']

    compile libDependencies['timber']

    compile libDependencies['finestwebview']

    compile(libDependencies['materialdrawer']) {
        transitive = true
    }

    compile(libDependencies['materialdialog-core']) {
        transitive = true
    }

    compile(libDependencies['materialdialog-commons']) {
        transitive = true
    }

    compile(libDependencies['recycler-fast-scroll']) {
        transitive = true
    }

    compile (libDependencies['pulltorefresh']) {
        exclude module: 'support-v4'
    }

    compile libDependencies['dagger']
    annotationProcessor libDependencies['dagger-compiler']
    provided libDependencies['jsr250-api']

    compile libDependencies['glide']
    compile libDependencies['jsoup']
    compile libDependencies['floatingactionbutton']
    compile libDependencies['subsampling-scale-image-view']
    provided libDependencies['tagsoup-osgi']
    compile libDependencies['persistentsearchview']

}

def Properties props = new Properties()
def propFile = new File('app/signing.properties')
if (propFile.canRead()){
    props.load(new FileInputStream(propFile))

    if (props!=null && props.containsKey('STORE_FILE') && props.containsKey('STORE_PASSWORD') &&
            props.containsKey('KEY_ALIAS') && props.containsKey('KEY_PASSWORD')) {
        android.signingConfigs.debug.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.debug.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.debug.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.debug.keyPassword = props['KEY_PASSWORD']
        android.signingConfigs.release.storeFile = file(props['STORE_FILE'])
        android.signingConfigs.release.storePassword = props['STORE_PASSWORD']
        android.signingConfigs.release.keyAlias = props['KEY_ALIAS']
        android.signingConfigs.release.keyPassword = props['KEY_PASSWORD']
    } else {
        println 'signing.properties found but some entries are missing'
        android.buildTypes.debug.signingConfig = null
        android.buildTypes.release.signingConfig = null
    }
} else {
    println 'signing.properties not found'
    android.buildTypes.debug.signingConfig = null
    android.buildTypes.release.signingConfig = null
}
